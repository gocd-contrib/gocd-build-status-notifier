/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java'
}

apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
    id = 'dummy'
    pluginVersion = '1.7.5'
    goCdVersion = '20.9.0'
    name = 'Wrapper build status notifier project'
    description = 'Wrapper build status notifier project'
    vendorName = 'GoCD Contributors'
    vendorUrl = 'https://github.com/gocd-contrib/gocd-build-status-notifier'

    githubRepo {
        owner = System.getenv('GITHUB_USER') ?: 'bob'
        repo = 'gocd-build-status-notifier'
        token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    }

    pluginProject = null // Plugins are defined in subprojects

    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    assetsToRelease = [
            project(':gerrit-cs-status').tasks.jar,
            project(':github-pr-status').tasks.jar,
            project(':gitlab-mr-status').tasks.jar,
            project(':stash-pr-status').tasks.jar
    ]
}

ext {
    deps = [
        gocdPluginApi: 'cd.go.plugin:go-plugin-api:25.3.0',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

allprojects {
    apply plugin: 'java'

    group = 'com.tw.go.plugin'

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

subprojects { Project sub ->
    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compileOnly project.deps.gocdPluginApi
        implementation 'com.google.code.gson:gson:2.13.2'
        implementation 'org.apache.httpcomponents:httpclient:4.5.14'

        constraints {
            implementation('commons-codec:commons-codec:1.19.0') {
                because 'Transitive dependency of apache httpclient has reported vulnerabilities'
            }
        }

        testImplementation project.deps.gocdPluginApi
        testImplementation 'org.mockito:mockito-core:5.20.0'
        testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.6'
        testImplementation platform('org.junit:junit-bom:6.0.0')
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
        testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
    }

    if (!sub.name.equalsIgnoreCase("common")) {
        apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

        jar {
            from(configurations.runtimeClasspath) {
                into "lib/"
            }
        }
    }
}

jar.enabled = false // No jar for the root project
